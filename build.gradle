plugins {
    id 'java'
    id 'pl.allegro.tech.build.axion-release' version '1.7.1'
    id 'nebula.ospackage' version '8.0.3'
}

apply plugin: 'java'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'nebula.ospackage'

sourceCompatibility = 1.8
defaultTasks 'clean', 'build'

configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile{
        extendsFrom pluginLibs
    }
}

scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = 'v'
        versionSeparator = ''
        def origDeserialize=deserialize

        //apend .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}
project.version = scmVersion.version

repositories {
    mavenCentral()
}

dependencies {
    pluginLibs group: 'org.freemarker', name: 'freemarker', version: '2.3.20'
    compile(group:'org.rundeck', name: 'rundeck-core', version: '2.8.2')
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    //include contents of output dir
    from "$buildDir/output"
    manifest {
        attributes 'Rundeck-Plugin-Name' : 'Slack Notification'
        attributes 'Rundeck-Plugin-Description' : 'Sends Rundeck notification messages to a slack channel.'
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '2.8.2+'
        attributes 'Rundeck-Plugin-Tags': 'java,notification,slack'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/slack-incoming-webhook-plugin'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Version': '1.2', 'Rundeck-Plugin-Archive': 'true','Rundeck-Plugin-File-Version': "${project.version}"
        //create space-separated list of pluginLibs
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
        attributes 'Rundeck-Plugin-Classnames': 'com.bitplaces.rundeck.plugins.slack.SlackNotificationPlugin', 'Rundeck-Plugin-Libs': "${libList}"
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

ospackage {
    summary 'Sends Rundeck notification messages to a slack channel'
    license 'Apache 2.0'
    requires 'rundeck'

    from(jar.outputs.files) {
        into 'var/lib/rundeck/libext'
    }
}
